plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'org.jetbrains.dokka' version "1.4.10.2"
    id 'maven-publish'
}

group 'ee.nx01.tonclient'
version '0.0.17'

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    publish = true

    pkg {
        repo = 'ton-client-kotlin'
        name = 'ee.nx01.tonclient'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mdorofeev/ton-client-kotlin.git'
        version {
            attributes = ['gradle-plugin': 'com.jfrog.bintray:com.jfrog.bintray.gradle:gradle-bintray-plugin']
        }
    }

}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/dokka/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'
    implementation 'org.scijava:native-lib-loader:2.3.4'
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.4.0"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.+"
    implementation("io.github.microutils:kotlin-logging:1.7.9")

    testImplementation 'io.kotest:kotest-runner-junit5:4.2.3'
    testImplementation 'io.kotest:kotest-assertions-core:4.2.3'
    testImplementation 'io.kotest:kotest-property:4.2.3'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.29'
}
