plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'com.adarshr.test-logger' version '2.1.0'
    id 'org.jetbrains.dokka' version "1.4.32"
    id 'maven'
    id 'signing'
}

group 'ee.nx-01.tonclient'


sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

def ossrhPassword = System.getenv('OSSRH_PASSWORD')


compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/dokka/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    def gpgKey = findProperty("gpgKey")
    def gpgPassword = System.getenv('GPG_PASSWORD')

    useInMemoryPgpKeys(gpgKey, gpgPassword)
    sign configurations.archives
}


uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://s01.oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ton-client-kotlin'
                packaging 'jar'

                description 'A application used as an example on how to set up pushing  its components to the Central Repository.'
                url 'https://github.com/mdorofeev/ton-client-kotlin'

                scm {
                    connection 'https://github.com/mdorofeev/ton-client-kotlin.git'
                    developerConnection 'https://github.com/mdorofeev/ton-client-kotlin.git'
                    url 'https://github.com/mdorofeev/ton-client-kotlin'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mdorofeev'
                        name 'Maxim Dorofeev'
                        email 'mdorofeev@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
    implementation 'org.scijava:native-lib-loader:2.4.0'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.6.20'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'
    implementation('io.github.microutils:kotlin-logging:2.1.21')

    testImplementation 'io.kotest:kotest-runner-junit5:5.2.3'
    testImplementation 'io.kotest:kotest-assertions-core:5.2.3'
    testImplementation 'io.kotest:kotest-property:5.2.3'
    testImplementation 'io.mockk:mockk:1.12.3'
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.29'
}
